sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources {
            srcDirs = ['src/intTest/resources']
        }
    }
}

configurations {
    // Configuration for integration tests
    intTestRuntime.extendsFrom testRuntime
    intTestImplementation.extendsFrom testImplementation
}

tasks.withType(Test).configureEach {
    // reset the report destinations so that intTests go to their own page
//    reports.html.destination = file(reporting.baseDirectory + '/' + name)
    reports.html.outputLocation = layout.buildDirectory.dir("reports/" + name)

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties as Map<String, ? extends Object>
}

tasks.register('intTest', Test) {
    // set the configuration context
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    // run the tests always
    outputs.upToDateWhen { false }
}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        // "STARTED",
    }
}

intTest {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        // "STARTED",
    }
}
